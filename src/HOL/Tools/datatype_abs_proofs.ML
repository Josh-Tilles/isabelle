(*  Title:      HOL/Tools/datatype_abs_proofs.ML
    ID:         $Id$
    Author:     Stefan Berghofer, TU Muenchen

Proofs and defintions independent of concrete representation
of datatypes  (i.e. requiring only abstract properties such as
injectivity / distinctness of constructors and induction)

 - case distinction (exhaustion) theorems
 - characteristic equations for primrec combinators
 - characteristic equations for case combinators
 - equations for splitting "P (case ...)" expressions
  - "nchotomy" and "case_cong" theorems for TFL

*)

signature DATATYPE_ABS_PROOFS =
sig
  val prove_casedist_thms : string list ->
    DatatypeAux.descr list -> (string * sort) list -> thm ->
    attribute list -> theory -> thm list * theory
  val prove_primrec_thms : bool -> string list ->
    DatatypeAux.descr list -> (string * sort) list ->
      DatatypeAux.datatype_info Symtab.table -> thm list list -> thm list list ->
        simpset -> thm -> theory -> (string list * thm list) * theory
  val prove_case_thms : bool -> string list ->
    DatatypeAux.descr list -> (string * sort) list ->
      string list -> thm list -> theory -> (thm list list * string list) * theory
  val prove_split_thms : string list ->
    DatatypeAux.descr list -> (string * sort) list ->
      thm list list -> thm list list -> thm list -> thm list list -> theory ->
        (thm * thm) list * theory
  val prove_nchotomys : string list -> DatatypeAux.descr list ->
    (string * sort) list -> thm list -> theory -> thm list * theory
  val prove_weak_case_congs : string list -> DatatypeAux.descr list ->
    (string * sort) list -> theory -> thm list * theory
  val prove_case_congs : string list ->
    DatatypeAux.descr list -> (string * sort) list ->
      thm list -> thm list list -> theory -> thm list * theory
end;

structure DatatypeAbsProofs: DATATYPE_ABS_PROOFS =
struct

open DatatypeAux;

(************************ case distinction theorems ***************************)

fun prove_casedist_thms new_type_names descr sorts induct case_names_exhausts thy =
  let
    val _ = message "Proving case distinction theorems ...";

    val descr' = List.concat descr;
    val recTs = get_rec_types descr' sorts;
    val newTs = Library.take (length (hd descr), recTs);

    val {maxidx, ...} = rep_thm induct;
    val induct_Ps = map head_of (HOLogic.dest_conj (HOLogic.dest_Trueprop (concl_of induct)));

    fun prove_casedist_thm ((i, t), T) =
      let
        val dummyPs = map (fn (Var (_, Type (_, [T', T'']))) =>
          Abs ("z", T', Const ("True", T''))) induct_Ps;
        val P = Abs ("z", T, HOLogic.imp $ HOLogic.mk_eq (Var (("a", maxidx+1), T), Bound 0) $
          Var (("P", 0), HOLogic.boolT))
        val insts = Library.take (i, dummyPs) @ (P::(Library.drop (i + 1, dummyPs)));
        val cert = cterm_of thy;
        val insts' = (map cert induct_Ps) ~~ (map cert insts);
        val induct' = refl RS ((List.nth
          (split_conj_thm (cterm_instantiate insts' induct), i)) RSN (2, rev_mp))

      in
        SkipProof.prove_global thy [] (Logic.strip_imp_prems t) (Logic.strip_imp_concl t)
          (fn prems => EVERY
            [rtac induct' 1,
             REPEAT (rtac TrueI 1),
             REPEAT ((rtac impI 1) THEN (eresolve_tac prems 1)),
             REPEAT (rtac TrueI 1)])
      end;

    val casedist_thms = map prove_casedist_thm ((0 upto (length newTs - 1)) ~~
      (DatatypeProp.make_casedists descr sorts) ~~ newTs)
  in
    thy
    |> store_thms_atts "exhaust" new_type_names (map single case_names_exhausts) casedist_thms
  end;


(*************************** primrec combinators ******************************)

fun prove_primrec_thms flat_names new_type_names descr sorts
    (dt_info : datatype_info Symtab.table) constr_inject dist_rewrites dist_ss induct thy =
  let
    val _ = message "Constructing primrec combinators ...";

    val big_name = space_implode "_" new_type_names;
    val thy0 = add_path flat_names big_name thy;

    val descr' = List.concat descr;
    val recTs = get_rec_types descr' sorts;
    val used = foldr add_typ_tfree_names [] recTs;
    val newTs = Library.take (length (hd descr), recTs);

    val induct_Ps = map head_of (HOLogic.dest_conj (HOLogic.dest_Trueprop (concl_of induct)));

    val big_rec_name' = big_name ^ "_rec_set";
    val rec_set_names' =
      if length descr' = 1 then [big_rec_name'] else
        map ((curry (op ^) (big_rec_name' ^ "_")) o string_of_int)
          (1 upto (length descr'));
    val rec_set_names = map (Sign.full_name thy0) rec_set_names';

    val (rec_result_Ts, reccomb_fn_Ts) = DatatypeProp.make_primrec_Ts descr sorts used;

    val rec_set_Ts = map (fn (T1, T2) =>
      reccomb_fn_Ts @ [T1, T2] ---> HOLogic.boolT) (recTs ~~ rec_result_Ts);

    val rec_fns = map (uncurry (mk_Free "f"))
      (reccomb_fn_Ts ~~ (1 upto (length reccomb_fn_Ts)));
    val rec_sets' = map (fn c => list_comb (Free c, rec_fns))
      (rec_set_names' ~~ rec_set_Ts);
    val rec_sets = map (fn c => list_comb (Const c, rec_fns))
      (rec_set_names ~~ rec_set_Ts);

    (* introduction rules for graph of primrec function *)

    fun make_rec_intr T rec_set ((rec_intr_ts, l), (cname, cargs)) =
      let
        fun mk_prem ((dt, U), (j, k, prems, t1s, t2s)) =
          let val free1 = mk_Free "x" U j
          in (case (strip_dtyp dt, strip_type U) of
             ((_, DtRec m), (Us, _)) =>
               let
                 val free2 = mk_Free "y" (Us ---> List.nth (rec_result_Ts, m)) k;
                 val i = length Us
               in (j + 1, k + 1, HOLogic.mk_Trueprop (HOLogic.list_all
                     (map (pair "x") Us, List.nth (rec_sets', m) $
                       app_bnds free1 i $ app_bnds free2 i)) :: prems,
                   free1::t1s, free2::t2s)
               end
           | _ => (j + 1, k, prems, free1::t1s, t2s))
          end;

        val Ts = map (typ_of_dtyp descr' sorts) cargs;
        val (_, _, prems, t1s, t2s) = foldr mk_prem (1, 1, [], [], []) (cargs ~~ Ts)

      in (rec_intr_ts @ [Logic.list_implies (prems, HOLogic.mk_Trueprop
        (rec_set $ list_comb (Const (cname, Ts ---> T), t1s) $
          list_comb (List.nth (rec_fns, l), t1s @ t2s)))], l + 1)
      end;

    val (rec_intr_ts, _) = Library.foldl (fn (x, ((d, T), set_name)) =>
      Library.foldl (make_rec_intr T set_name) (x, #3 (snd d)))
        (([], 0), descr' ~~ recTs ~~ rec_sets');

    val ({intrs = rec_intrs, elims = rec_elims, ...}, thy1) =
        InductivePackage.add_inductive_global (serial_string ())
          {quiet_mode = ! quiet_mode, verbose = false, kind = Thm.internalK,
            alt_name = big_rec_name', coind = false, no_elim = false, no_ind = true,
            skip_mono = true}
          (map (fn (s, T) => ((s, T), NoSyn)) (rec_set_names' ~~ rec_set_Ts))
          (map dest_Free rec_fns)
          (map (fn x => (("", []), x)) rec_intr_ts) [] thy0;

    (* prove uniqueness and termination of primrec combinators *)

    val _ = message "Proving termination and uniqueness of primrec functions ...";

    fun mk_unique_tac ((tac, intrs), ((((i, (tname, _, constrs)), elim), T), T')) =
      let
        val distinct_tac =
          (if i < length newTs then
             full_simp_tac (HOL_ss addsimps (List.nth (dist_rewrites, i))) 1
           else full_simp_tac dist_ss 1);

        val inject = map (fn r => r RS iffD1)
          (if i < length newTs then List.nth (constr_inject, i)
            else #inject (the (Symtab.lookup dt_info tname)));

        fun mk_unique_constr_tac n ((tac, intr::intrs, j), (cname, cargs)) =
          let
            val k = length (List.filter is_rec_type cargs)

          in (EVERY [DETERM tac,
                REPEAT (etac ex1E 1), rtac ex1I 1,
                DEPTH_SOLVE_1 (ares_tac [intr] 1),
                REPEAT_DETERM_N k (etac thin_rl 1 THEN rotate_tac 1 1),
                etac elim 1,
                REPEAT_DETERM_N j distinct_tac,
                TRY (dresolve_tac inject 1),
                REPEAT (etac conjE 1), hyp_subst_tac 1,
                REPEAT (EVERY [etac allE 1, dtac mp 1, atac 1]),
                TRY (hyp_subst_tac 1),
                rtac refl 1,
                REPEAT_DETERM_N (n - j - 1) distinct_tac],
              intrs, j + 1)
          end;

        val (tac', intrs', _) = Library.foldl (mk_unique_constr_tac (length constrs))
          ((tac, intrs, 0), constrs);

      in (tac', intrs') end;

    val rec_unique_thms =
      let
        val rec_unique_ts = map (fn (((set_t, T1), T2), i) =>
          Const ("Ex1", (T2 --> HOLogic.boolT) --> HOLogic.boolT) $
            absfree ("y", T2, set_t $ mk_Free "x" T1 i $ Free ("y", T2)))
              (rec_sets ~~ recTs ~~ rec_result_Ts ~~ (1 upto length recTs));
        val cert = cterm_of thy1
        val insts = map (fn ((i, T), t) => absfree ("x" ^ (string_of_int i), T, t))
          ((1 upto length recTs) ~~ recTs ~~ rec_unique_ts);
        val induct' = cterm_instantiate ((map cert induct_Ps) ~~
          (map cert insts)) induct;
        val (tac, _) = Library.foldl mk_unique_tac
          (((rtac induct' THEN_ALL_NEW ObjectLogic.atomize_prems_tac) 1
              THEN rewtac (mk_meta_eq choice_eq), rec_intrs),
            descr' ~~ rec_elims ~~ recTs ~~ rec_result_Ts);

      in split_conj_thm (SkipProof.prove_global thy1 [] []
        (HOLogic.mk_Trueprop (mk_conj rec_unique_ts)) (K tac))
      end;

    val rec_total_thms = map (fn r => r RS theI') rec_unique_thms;

    (* define primrec combinators *)

    val big_reccomb_name = (space_implode "_" new_type_names) ^ "_rec";
    val reccomb_names = map (Sign.full_name thy1)
      (if length descr' = 1 then [big_reccomb_name] else
        (map ((curry (op ^) (big_reccomb_name ^ "_")) o string_of_int)
          (1 upto (length descr'))));
    val reccombs = map (fn ((name, T), T') => list_comb
      (Const (name, reccomb_fn_Ts @ [T] ---> T'), rec_fns))
        (reccomb_names ~~ recTs ~~ rec_result_Ts);

    val (reccomb_defs, thy2) =
      thy1
      |> Sign.add_consts_i (map (fn ((name, T), T') =>
          (Sign.base_name name, reccomb_fn_Ts @ [T] ---> T', NoSyn))
          (reccomb_names ~~ recTs ~~ rec_result_Ts))
      |> (PureThy.add_defs_i false o map Thm.no_attributes) (map (fn ((((name, comb), set), T), T') =>
          ((Sign.base_name name) ^ "_def", Logic.mk_equals (comb, absfree ("x", T,
           Const ("The", (T' --> HOLogic.boolT) --> T') $ absfree ("y", T',
             set $ Free ("x", T) $ Free ("y", T'))))))
               (reccomb_names ~~ reccombs ~~ rec_sets ~~ recTs ~~ rec_result_Ts))
      ||> parent_path flat_names;


    (* prove characteristic equations for primrec combinators *)

    val _ = message "Proving characteristic theorems for primrec combinators ..."

    val rec_thms = map (fn t => SkipProof.prove_global thy2 [] [] t
      (fn _ => EVERY
        [rewrite_goals_tac reccomb_defs,
         rtac the1_equality 1,
         resolve_tac rec_unique_thms 1,
         resolve_tac rec_intrs 1,
         REPEAT (rtac allI 1 ORELSE resolve_tac rec_total_thms 1)]))
           (DatatypeProp.make_primrecs new_type_names descr sorts thy2)

  in
    thy2
    |> Sign.add_path (space_implode "_" new_type_names)
    |> PureThy.add_thmss [(("recs", rec_thms), [])]
    ||> Sign.parent_path
    |-> (fn thms => pair (reccomb_names, Library.flat thms))
  end;


(***************************** case combinators *******************************)

fun prove_case_thms flat_names new_type_names descr sorts reccomb_names primrec_thms thy =
  let
    val _ = message "Proving characteristic theorems for case combinators ...";

    val thy1 = add_path flat_names (space_implode "_" new_type_names) thy;

    val descr' = List.concat descr;
    val recTs = get_rec_types descr' sorts;
    val used = foldr add_typ_tfree_names [] recTs;
    val newTs = Library.take (length (hd descr), recTs);
    val T' = TFree (Name.variant used "'t", HOLogic.typeS);

    fun mk_dummyT dt = binder_types (typ_of_dtyp descr' sorts dt) ---> T';

    val case_dummy_fns = map (fn (_, (_, _, constrs)) => map (fn (_, cargs) =>
      let
        val Ts = map (typ_of_dtyp descr' sorts) cargs;
        val Ts' = map mk_dummyT (List.filter is_rec_type cargs)
      in Const ("arbitrary", Ts @ Ts' ---> T')
      end) constrs) descr';

    val case_names = map (fn s => Sign.full_name thy1 (s ^ "_case")) new_type_names;

    (* define case combinators via primrec combinators *)

    val (case_defs, thy2) = Library.foldl (fn ((defs, thy),
      ((((i, (_, _, constrs)), T), name), recname)) =>
        let
          val (fns1, fns2) = ListPair.unzip (map (fn ((_, cargs), j) =>
            let
              val Ts = map (typ_of_dtyp descr' sorts) cargs;
              val Ts' = Ts @ map mk_dummyT (List.filter is_rec_type cargs);
              val frees' = map (uncurry (mk_Free "x")) (Ts' ~~ (1 upto length Ts'));
              val frees = Library.take (length cargs, frees');
              val free = mk_Free "f" (Ts ---> T') j
            in
             (free, list_abs_free (map dest_Free frees',
               list_comb (free, frees)))
            end) (constrs ~~ (1 upto length constrs)));

          val caseT = (map (snd o dest_Free) fns1) @ [T] ---> T';
          val fns = (List.concat (Library.take (i, case_dummy_fns))) @
            fns2 @ (List.concat (Library.drop (i + 1, case_dummy_fns)));
          val reccomb = Const (recname, (map fastype_of fns) @ [T] ---> T');
          val decl = (Sign.base_name name, caseT, NoSyn);
          val def = ((Sign.base_name name) ^ "_def",
            Logic.mk_equals (list_comb (Const (name, caseT), fns1),
              list_comb (reccomb, (List.concat (Library.take (i, case_dummy_fns))) @
                fns2 @ (List.concat (Library.drop (i + 1, case_dummy_fns))) )));
          val ([def_thm], thy') =
            thy
            |> Sign.declare_const [] decl |> snd
            |> (PureThy.add_defs_i false o map Thm.no_attributes) [def];

        in (defs @ [def_thm], thy')
        end) (([], thy1), (hd descr) ~~ newTs ~~ case_names ~~
          (Library.take (length newTs, reccomb_names)));

    val case_thms = map (map (fn t => SkipProof.prove_global thy2 [] [] t
      (fn _ => EVERY [rewrite_goals_tac (case_defs @ map mk_meta_eq primrec_thms), rtac refl 1])))
          (DatatypeProp.make_cases new_type_names descr sorts thy2)

  in
    thy2
    |> parent_path flat_names
    |> store_thmss "cases" new_type_names case_thms
    |-> (fn thmss => pair (thmss, case_names))
  end;


(******************************* case splitting *******************************)

fun prove_split_thms new_type_names descr sorts constr_inject dist_rewrites
    casedist_thms case_thms thy =
  let
    val _ = message "Proving equations for case splitting ...";

    val descr' = List.concat descr;
    val recTs = get_rec_types descr' sorts;
    val newTs = Library.take (length (hd descr), recTs);

    fun prove_split_thms ((((((t1, t2), inject), dist_rewrites'),
        exhaustion), case_thms'), T) =
      let
        val cert = cterm_of thy;
        val _ $ (_ $ lhs $ _) = hd (Logic.strip_assums_hyp (hd (prems_of exhaustion)));
        val exhaustion' = cterm_instantiate
          [(cert lhs, cert (Free ("x", T)))] exhaustion;
        val tacf = K (EVERY [rtac exhaustion' 1, ALLGOALS (asm_simp_tac
          (HOL_ss addsimps (dist_rewrites' @ inject @ case_thms')))])
      in
        (SkipProof.prove_global thy [] [] t1 tacf,
         SkipProof.prove_global thy [] [] t2 tacf)
      end;

    val split_thm_pairs = map prove_split_thms
      ((DatatypeProp.make_splits new_type_names descr sorts thy) ~~ constr_inject ~~
        dist_rewrites ~~ casedist_thms ~~ case_thms ~~ newTs);

    val (split_thms, split_asm_thms) = ListPair.unzip split_thm_pairs

  in
    thy
    |> store_thms "split" new_type_names split_thms
    ||>> store_thms "split_asm" new_type_names split_asm_thms
    |-> (fn (thms1, thms2) => pair (thms1 ~~ thms2))
  end;

fun prove_weak_case_congs new_type_names descr sorts thy =
  let
    fun prove_weak_case_cong t =
       SkipProof.prove_global thy [] (Logic.strip_imp_prems t) (Logic.strip_imp_concl t)
         (fn prems => EVERY [rtac ((hd prems) RS arg_cong) 1])

    val weak_case_congs = map prove_weak_case_cong (DatatypeProp.make_weak_case_congs
      new_type_names descr sorts thy)

  in thy |> store_thms "weak_case_cong" new_type_names weak_case_congs end;

(************************* additional theorems for TFL ************************)

fun prove_nchotomys new_type_names descr sorts casedist_thms thy =
  let
    val _ = message "Proving additional theorems for TFL ...";

    fun prove_nchotomy (t, exhaustion) =
      let
        (* For goal i, select the correct disjunct to attack, then prove it *)
        fun tac i 0 = EVERY [TRY (rtac disjI1 i),
              hyp_subst_tac i, REPEAT (rtac exI i), rtac refl i]
          | tac i n = rtac disjI2 i THEN tac i (n - 1)
      in 
        SkipProof.prove_global thy [] [] t (fn _ =>
          EVERY [rtac allI 1,
           exh_tac (K exhaustion) 1,
           ALLGOALS (fn i => tac i (i-1))])
      end;

    val nchotomys =
      map prove_nchotomy (DatatypeProp.make_nchotomys descr sorts ~~ casedist_thms)

  in thy |> store_thms "nchotomy" new_type_names nchotomys end;

fun prove_case_congs new_type_names descr sorts nchotomys case_thms thy =
  let
    fun prove_case_cong ((t, nchotomy), case_rewrites) =
      let
        val (Const ("==>", _) $ tm $ _) = t;
        val (Const ("Trueprop", _) $ (Const ("op =", _) $ _ $ Ma)) = tm;
        val cert = cterm_of thy;
        val nchotomy' = nchotomy RS spec;
        val nchotomy'' = cterm_instantiate
          [(cert (hd (add_term_vars (concl_of nchotomy', []))), cert Ma)] nchotomy'
      in
        SkipProof.prove_global thy [] (Logic.strip_imp_prems t) (Logic.strip_imp_concl t)
          (fn prems => 
            let val simplify = asm_simp_tac (HOL_ss addsimps (prems @ case_rewrites))
            in EVERY [simp_tac (HOL_ss addsimps [hd prems]) 1,
                cut_facts_tac [nchotomy''] 1,
                REPEAT (etac disjE 1 THEN REPEAT (etac exE 1) THEN simplify 1),
                REPEAT (etac exE 1) THEN simplify 1 (* Get last disjunct *)]
            end)
      end;

    val case_congs = map prove_case_cong (DatatypeProp.make_case_congs
      new_type_names descr sorts thy ~~ nchotomys ~~ case_thms)

  in thy |> store_thms "case_cong" new_type_names case_congs end;

end;
